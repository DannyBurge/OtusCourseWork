plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.dagger.hilt)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.detekt)
}

android {
    namespace 'com.otuscoursework'
    compileSdkVersion libs.versions.compileSdk.get() as Integer
    buildToolsVersion libs.versions.buildToolsVersion.get()

    defaultConfig {
        applicationId "com.otuscoursework"
        minSdk libs.versions.minSdk.get() as Integer
        targetSdk libs.versions.targetSdk.get() as Integer
        versionCode libs.versions.versionCode.get() as Integer
        versionName libs.versions.appVersion.get()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        testOptions {
            animationsDisabled = true
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = libs.versions.jvmVersion.get()
    }
}

kapt {
    correctErrorTypes true
}

dependencies {
    implementation project(':source:ui')

    // AndroidX
    implementation libs.bundles.androidx

    // Material Design Components
    implementation libs.android.material

    // Dagger Hilt
    implementation libs.hilt.android
    kapt libs.hilt.compiler

    // For instrumented tests
    androidTestImplementation libs.test.espressoCore
    androidTestImplementation libs.test.ext

    detektPlugins libs.detekt.formatting
}

detekt {
    config = files("$rootDir/config/detekt/detekt.yml")
    buildUponDefaultConfig = true
    ignoreFailures = true
}